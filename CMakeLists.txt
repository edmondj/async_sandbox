cmake_minimum_required(VERSION 3.15)

project("async_sandbox")

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED On)

function(organize_targets_in_rec FOLDER_NAME DIR)
  get_property(TARGETS DIRECTORY ${DIR} PROPERTY BUILDSYSTEM_TARGETS)
  foreach(CUR_TARGET IN LISTS TARGETS)
    get_target_property(CUR_FOLDER ${CUR_TARGET} FOLDER)
    if (${CUR_FOLDER} STREQUAL "CUR_FOLDER-NOTFOUND")
      set(NEW_FOLDER ${FOLDER_NAME})
    else ()
      set(NEW_FOLDER "${FOLDER_NAME}/${CUR_FOLDER}")
    endif ()
    set_target_properties(${CUR_TARGET} PROPERTIES FOLDER ${NEW_FOLDER})
  endforeach()
  get_property(SUBDIRS DIRECTORY ${DIR} PROPERTY SUBDIRECTORIES)
  foreach(SUBDIR IN LISTS SUBDIRS)
    organize_targets_in_rec(${FOLDER_NAME} ${SUBDIR})
  endforeach()
endfunction()

function(organize_targets_in FOLDER_NAME)
  organize_targets_in_rec(${FOLDER_NAME} ${CMAKE_CURRENT_SOURCE_DIR})
endfunction()

find_program(iwyu_path NAMES include-what-you-use iwyu)
if (iwyu_path)
  message("Found include-what-you-use: ${iwyu_path}")
endif ()

function(setup_target_compile_options t)
  if (MSVC)
    target_compile_options(${t} PRIVATE "/W3" "/WX")
  elseif (CMAKE_CXX_COMPILER_ID MATCHES "(GNU|Clang)")
    target_compile_options(${t} PRIVATE "-Wextra" "-Wall" "-Werror")
    target_compile_options(${t} PRIVATE "-fsanitize=address")
    target_link_options(${t} PRIVATE "-fsanitize=address")
  endif ()
#  if (iwyu_path)
#    set_property(TARGET ${t} PROPERTY CXX_INCLUDE_WHAT_YOU_USE ${iwyu_path})
#  endif ()
endfunction()

add_subdirectory("utils")
add_subdirectory("async_grpc")
add_subdirectory("game")
