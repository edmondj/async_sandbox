cmake_minimum_required(VERSION 3.15)

project("async_sandbox")

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED On)

option(ASYNC_LIB_ASAN "Build with address sanitizer")
option(ASYNC_LIB_IWYU "Run include-what-you-use (if found)")
option(ASYNC_LIB_GRPC "Build the grpc lib" true)
option(ASYNC_LIB_EXAMPLES "Build the example programs (some may need ASYNC_LIB_GRPC)" true)
option(ASYNC_LIB_EXCEPTIONS "Build with exceptions enabled" true)
option(ASYNC_LIB_RTTI "Build with runtime type info enabled" true)

function(organize_targets_in_rec FOLDER_NAME DIR)
  get_property(TARGETS DIRECTORY ${DIR} PROPERTY BUILDSYSTEM_TARGETS)
  foreach(CUR_TARGET IN LISTS TARGETS)
    get_target_property(CUR_FOLDER ${CUR_TARGET} FOLDER)
    if (${CUR_FOLDER} STREQUAL "CUR_FOLDER-NOTFOUND")
      set(NEW_FOLDER ${FOLDER_NAME})
    else ()
      set(NEW_FOLDER "${FOLDER_NAME}/${CUR_FOLDER}")
    endif ()
    set_target_properties(${CUR_TARGET} PROPERTIES FOLDER ${NEW_FOLDER})
  endforeach()
  get_property(SUBDIRS DIRECTORY ${DIR} PROPERTY SUBDIRECTORIES)
  foreach(SUBDIR IN LISTS SUBDIRS)
    organize_targets_in_rec(${FOLDER_NAME} ${SUBDIR})
  endforeach()
endfunction()

function(organize_targets_in FOLDER_NAME)
  organize_targets_in_rec(${FOLDER_NAME} ${CMAKE_CURRENT_SOURCE_DIR})
endfunction()

find_program(iwyu_path NAMES include-what-you-use iwyu)
if (iwyu_path)
  message("Found include-what-you-use: ${iwyu_path}")
endif ()

function(setup_target_compile_options t)
  if (MSVC)
    target_compile_options(${t} PRIVATE "/W3" "/WX")
    if (ASYNC_LIB_ASAN)
      target_compile_options(${t} PRIVATE "/fsanitize=address")
    endif ()
    if (NOT ASYNC_LIB_EXCEPTIONS)
      target_compile_options(${t} PRIVATE "/EHsc")
    endif ()
    if (NOT ASYNC_LIB_RTTI)
      target_compile_options(${t} PRIVATE "/GR-")
    endif ()
  elseif (CMAKE_CXX_COMPILER_ID MATCHES "(GNU|Clang)")
    target_compile_options(${t} PRIVATE "-Wextra" "-Wall" "-Werror")
    if (ASYNC_LIB_ASAN)
      target_compile_options(${t} PRIVATE "-fsanitize=address")
      target_link_options(${t} PRIVATE "-fsanitize=address")
    endif ()
    if (NOT ASYNC_LIB_EXCEPTIONS)
      target_compile_options(${t} PRIVATE "-fno-exceptions")
    endif ()
    if (NOT ASYNC_LIB_RTTI)
      target_compile_options(${t} PRIVATE "-fno-rtti")
    endif ()
  endif ()
  if (ASYNC_LIB_IWYU AND iwyu_path)
    set_property(TARGET ${t} PROPERTY CXX_INCLUDE_WHAT_YOU_USE ${iwyu_path})
  endif ()
endfunction()

add_subdirectory("utils")
add_subdirectory("async_lib")
if (ASYNC_LIB_GRPC)
  add_subdirectory("async_grpc")
  if (ASYNC_LIB_EXAMPLES)
    add_subdirectory("game")
  endif ()
endif ()
